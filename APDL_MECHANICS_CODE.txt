
FINISH
/CLEAR, START, NEW
/FILNAM,road_sign
/TITLE,simply first version

! >>>>> VERTICAL PILLAR PARAMETERS <<<<<

L1 = 6800			! pillar length, mm
B1 = 300			! low cross section width, mm
H1 = 900			! low cross section height, mm
B2 = 600			! high cross section width, mm
H2 = 600			! high cross section height, mm
T1 = 10				! thickness, mm

! >>>>> HORIZONTAL BEAM PARAMETERS <<<<<

! length2= 1420			! first beam length, mm
! length3= 6000			! second beam length, mm
L2 = 7420			! pillar length, mm
B3 = 400			! high cross section width, mm
H3 = 600			! high cross section height, mm
T2 = 8				! thickness, mm

! >>>>> PLATE PARAMETERS <<<<<
T3 = 30				! thickness, mm

! >>>>> RACKETS PARAMETERS <<<<<

Lrackdown= 1200			! racket length, mm
Lrackup= 3100			! racket length, mm
B4 = 60				! cross section width, mm
H4 = 100			! cross section height, mm

d1 = 1390 + 2130 + 350				! distances, mm
d2 = 800 							! distances, mm
d3 = 850							! distances, mm
d4 = 850							! distances, mm
d5 = 800							! distances, mm
d6 = L2 - d1 - d2 - d3 - d4 - d5	! distances, mm
! length6 = 2130			! distances, mm
! length7 = 350			! distances, mm
! d2 = 800			! distances, mm
! d3 = 850			! distances, mm


! >>>>> MATERIAL PROPERTIES <<<<<
E_Young = 210000		! Young's modulus, MPa
ni = 0.29			! Poisson's ratio
Density=7.85e-6		! Kg/mm3


! >>>>> other parameters <<<<<
UpF = 7690	   ! load (N/mm)
LwF = 3130	   ! load (N/mm)
E_length = 100	! element length (mm)
Eps = 1E-4	! selection tolerance

/PREP7
ET, 1, BEAM189
!section of low pillar
SECTYPE,   1, BEAM, HREC, low_pillar, 0  
SECOFFSET, USER, 150, 450   	! Change the origin axis point
SECDATA,B1,H1,T1,T1,T1,T1,0,0,0,0,0,0 

!section of high pillar
SECTYPE,   2, BEAM, HREC, high_pillar, 0  
SECOFFSET, USER, 400, 300   	! Change the origin axis point
SECDATA,B2,H2,T1,T1,T1,T1,0,0,0,0,0,0 

!section of horizontal beam
SECTYPE,   3, BEAM, HREC, horizontal_beam, 0   
SECDATA,B3,H3,T2,T2,T2,T2,0,0,0,0,0,0 

!Section of taper beam
SECTYPE,   4, TAPER, , taper_pillar
SECDATA,   1,,,,
SECDATA,   2,0,L1,,

!Section of the rackets
SECTYPE,   5, BEAM, RECT,racket,0
SECDATA,B4,H4

!Material definition
MP, EX, 1, E_Young
MP, PRXY, 1, ni
MP,dens,1, density

!keypoints pillar
K,1,0,0,0
K,2,0,L1,0 
L, 1, 2  

LESIZE, all,E_length
LATT,1,,1,,,,4
LMESH, 1

Lsel,u,line,,all

!keypoint first h. beam
K,3, -B1/2, L1+B3/2,0
K,4, 0, L1+B3/2,0
K,5, d1, L1+B3/2,0
K,6, d1+d2, L1+B3/2,0
K,7, d1+d2+d3, L1+B3/2,0
K,8, d1+d2+d3+d4, L1+B3/2,0
K,9, d1+d2+d3+d4+d5, L1+B3/2,0
K,10,L2,L1+B3/2,0
L,3,4
L, 4, 5
L, 5, 6
L, 6, 7
L, 7, 8
L, 8, 9
L, 9, 10

LESIZE,all,E_length
Latt,1,,1,,,,3
LMESH,all
Lsel,u,line,,all

! >>>> 	view setting: 	<<<<
/SHRINK,0   
/ESHAPE,1.0 
/EFACET,1   
/RATIO,1,1,1
/CFORMAT,32,0   
/REPLOT 



!keypoints for rackets

K,11,d1,L1+B3/2,H3/2+H4/2
K,12,d1,L1+B3/2-Lrackdown/2,H3/2+H4/2	!lower point of force
K,13,d1,L1+B3/2-Lrackdown,H3/2+H4/2
K,14,d1,L1+B3/2+Lrackup/2,H3/2+H4/2	!upper point of force
K,15,d1,L1+B3/2+Lrackup,H3/2+H4/2

L, 11, 12
L, 12, 13
L, 11, 14
L, 14, 15

K,16,d1+d2,L1+B3/2,H3/2+H4/2
K,17,d1+d2,L1+B3/2-Lrackdown/2,H3/2+H4/2	!lower point of force
K,18,d1+d2,L1+B3/2-Lrackdown,H3/2+H4/2
K,19,d1+d2,L1+B3/2+Lrackup/2,H3/2+H4/2	!upper point of force
K,20,d1+d2,L1+B3/2+Lrackup,H3/2+H4/2

L, 16, 17
L, 17, 18
L, 16, 19
L, 19, 20

K,21,d1+d2+d3,L1+B3/2,H3/2+H4/2
K,22,d1+d2+d3,L1+B3/2-Lrackdown/2,H3/2+H4/2	!lower point of force
K,23,d1+d2+d3,L1+B3/2-Lrackdown,H3/2+H4/2
K,24,d1+d2+d3,L1+B3/2+Lrackup/2,H3/2+H4/2	!upper point of force
K,25,d1+d2+d3,L1+B3/2+Lrackup,H3/2+H4/2

L, 21, 22
L, 22, 23
L, 21, 24
L, 24, 25

K,26,d1+d2+d3+d4,L1+B3/2,H3/2+H4/2
K,27,d1+d2+d3+d4,L1+B3/2-Lrackdown/2,H3/2+H4/2	!lower point of force
K,28,d1+d2+d3+d4,L1+B3/2-Lrackdown,H3/2+H4/2
K,29,d1+d2+d3+d4,L1+B3/2+Lrackup/2,H3/2+H4/2	!upper point of force
K,30,d1+d2+d3+d4,L1+B3/2+Lrackup,H3/2+H4/2

L, 26, 27
L, 27, 28
L, 26, 29
L, 29, 30

K,31,d1+d2+d3+d4+d5,L1+B3/2,H3/2+H4/2
K,32,d1+d2+d3+d4+d5,L1+B3/2-Lrackdown/2,H3/2+H4/2	!lower point of force
K,33,d1+d2+d3+d4+d5,L1+B3/2-Lrackdown,H3/2+H4/2
K,34,d1+d2+d3+d4+d5,L1+B3/2+Lrackup/2,H3/2+H4/2	!upper point of force
K,35,d1+d2+d3+d4+d5,L1+B3/2+Lrackup,H3/2+H4/2

L, 31, 32
L, 32, 33
L, 31, 34
L, 34, 35

LESIZE,all,E_length
Latt,1,,1,,,,5
LMESH,all
Lsel,u,line,,all


!keypoints for rigid plates
K, 36,0,L1,0 
K, 37,d1,L1+B3/2,H3/2+H4/2
K, 38,d1+d2,L1+B3/2,H3/2+H4/2
K, 39,d1+d2+d3,L1+B3/2,H3/2+H4/2
K, 40,d1+d2+d3+d4,L1+B3/2,H3/2+H4/2
K, 41,d1+d2+d3+d4+d5,L1+B3/2,H3/2+H4/2
L, 36, 4
L, 37, 5
L, 38, 6
L, 39, 7
L, 40, 8
L, 41, 9

ET,2,184,1,1
TYPE,2
LESIZE, all,E_length
LMESH, all
allsel,all


!use of CPINTF     !Internal pins
CPINTF,ux,eps
CPINTF,uy,eps
CPINTF,uz,eps
CPINTF,rotx,eps
CPINTF,roty,eps
CPINTF,rotz,eps

ALLSEL, all
save
Fini

/SOLUTION
ANTYPE, STATIC, NEW   !static solution

!Fixed base
NSEL,S,loc,y,-eps,+eps
D, ALL,ALL
allsel,all

ksel,s,kp,,12,32,5
nslk,s
F,all,Fz,-LwF

ksel,s,kp,,14,34,5
nslk,s
F,all,Fz,-UpF

ALLSEL, all
!Gravitational acceleration
Acel,,9.81	!m/s2

allsel,all,all
solve
save
fini

/POST1
set,last
ETABLE, axI, SMISC, 1 		!axial force I
ETABLE, axJ, SMISC, 14 		!axial force J
ETABLE, shearyI, SMISC, 6 	!shear I
ETABLE, shearyJ, SMISC, 19 	!shear J
ETABLE, shearzI, SMISC, 5	!shear I
ETABLE, shearzj, SMISC, 18 	!shear J
ETABLE, momxI, SMISC, 4 	!moment z in I
ETABLE, momxJ, SMISC, 17 	!moment z in J
ETABLE, momzI, SMISC, 3 	!moment z in I
ETABLE, momzJ, SMISC, 16 	!moment z in J
ETABLE, momyI, SMISC, 2 	!moment z in I
ETABLE, momyJ, SMISC, 15 	!moment z in J
ETABLE, bsigmaI, SMISC, 32 	!bending stress y in I
ETABLE, bsigmaJ, SMISC, 37 	!bending stress y in J
ETABLE, asigmaI, SMISC, 31 	!axial stress in I
ETABLE, asigmaJ, SMISC, 36 	!axial stress in J
ETABLE, csigmaI, SMISC, 34 	!bending stress z in I
ETABLE, csigmaJ, SMISC, 39 	!bending stress z in J

PLLS, axI, axJ
PLLS, shearyI, shearyJ
PLLS, shearzI, shearzJ
PLLS, momxI, momxJ
PLLS, momyI, momyJ
PLLS, momzI, momzJ
PLLS, bsigmaI, bsigmaJ
PLLS, asigmaI, asigmaJ
 

PLDISP, 1					! Plot the displaced structure