FINISH
/CLEAR, START, NEW
/FILNAM,base_plate
/TITLE, analysis of a plate


!------------------------------------!
!!>>>>>>>>>>> PARAMETERS <<<<<<<<<<<!!
!------------------------------------!

! >> Plate dimensions <<
R1 = 10				! In and out fillet radius, < 7 of the welding
tk = 10				! Thickness of the vertical plate 
tko = 30			! Thickness of the orizontal plate
B = 110				! Plate width
H = 100
B1 = 30				! P1P2
B2 = B - B1			! P2P3
B3 = B1-tk-R1 			! P5P6 
R2 = 7				! Corner radius
L1half = 880 / 2 		! Half of the major length of the plate
L2half = 280 / 2 		! Half of the minor length of the plate
Planewidth = 90			! Plane dimension without considering the first plane width
L1division=L1half/44		! L1 division to match horizontal and vertical mash
L2division=L2half/14		! L2 division to match horizontal and vertical mash

! >> Hole dimensions <<
Rh = 15				! Hole radius
x_ctr = 210			! x coordinate of the centers along L1half
z_ctr = 170*3			! z coordinate of the centers along L2half
hdist_vert = 170		! Distance between the centers along L1half
hdist_hor = 105			! Distance between the centers along L2half

! >> M27 Nut dimensions <<
nut_hole_radius = 27/2 		! d
nut_height = 22			! m
nut_ext_radius = 45.63/2	! e

Eps = 1E-4	! selection tolerance


! >> Material properties <<
E_Young = 210000		! Young's modulus, MPa
ni = 0.29			! Poisson's ratio

! >> Other parameters <<
pi = acos(-1)
COS45 = cos(pi/4)
Ndivl1 = 4
Ndivl2 = 5
Ndivl3 = 10
Ndivtk = 3
Ndivcirc = 4
Sratio = 2

! >> Force definition << 

Sigmay = -2 ! -31000/23600
Mfx = -4.25e8
Mty = 2.99e8		!vertical axis
Mfz = -8.85e7		!eing axis
Preload = 100000		!Mpa


!------------------------------------!
!!>>>>>>>> MODEL DEFINITION <<<<<<<<!!
!------------------------------------!

/prep7
MP,EX,1,E_Young
MP,PRXY,1,ni

ET,1,186
MP,MU,7,0.5
MP,MU,1,0.5


! >> Vertical section <<

! Key points
K, 1, L2half+R2, 0, 0
K, 2, L2half+B1+R2, 0, 0
K, 3, L2half+B1+R2, tko, 0
K, 4, L2half+tk+R1+R2, tko, 0
K, 5, L2half+tk+R2, tko+R1, 0
K, 6, L2half+tk+R2, H, 0
K, 7, L2half+R2, H, 0
K, 8, L2half+R2, tko+R1, 0
K, 9, L2half+R2, 0, L1half ! To extrude the plate

! Lines
L,1,2 		! 1
L,2,3 		! 2
L,3,4 		! 3

LOCAL, 11, 1, L2half+B1-B3+R2,tko+R1,0
CSYS, 11
L,4,5 		! 4

CSYS, 0
L,5,6 		! 5
L,6,7 		! 6
L,7,8 		! 7
L,8,1		! 8

! Lines to creat sub areas
L,1,4		! 9
L,5,8 		! 10

! Lines to extrude
L,1,9		! 11

! LINE DIVISION
LSEL,S,LINE,,1,3,2		! 1-2 3-4
LESIZE,ALL,,,Ndivl1

LSEL,S,LINE,,4,8,4     		! 4-5 8-1
LESIZE,ALL,,,Ndivl2

LSEL,S,LINE,,5,7,2      	! 5-6 7-8
LESIZE,ALL,,,Ndivl2

LSEL,S,LINE,,2,9,7      	! 2-3 1-4
LSEL,A,LINE,,6,10,4   		! 5-8 6-7
LESIZE,ALL,,,Ndivtk

LSEL,S,LINE,,11		    	! 1-9
LESIZE,ALL,,,L1half/L1division
ALLSEL,ALL

! Areas
AL, 1, 2, 3, 9    	! 1
AL, 9, 4, 10, 8		! 2
AL, 10, 5, 6, 7		! 3


! Extrude along L2half
VDRAG,ALL,,,,,,11

! Revolution of a part of the section
! around the axis at 7 mm from the edge

! Curve line to extrude 
LOCAL, 12, 1, L2half,0,L1half,, 90
CSYS, 12

K, 50, R2, 0, 0
K, 51, R2, 90, 0
L, 50, 51 
ncline = _return

LESIZE,ncline ,,,2*Ndivl1
VDRAG, 8,12,16,,,,ncline

CSYS, 0

NUMMRG,ALL
NUMCMP,ALL

! Extrusion along L1half
K, 25, 0, 0, L1half +R2

L, 17, 25
nlocal = _return
LESIZE,nlocal,,,L2half/L2division

VDRAG, 21, 25, 29,,,, nlocal

! Reorder and renumbering all entities
NUMMRG,ALL
NUMCMP,ALL

!------------------------------------!
!!>>>>>>>> Horizontal Plate <<<<<<<<!!
!------------------------------------!

! External plate points
K, 50, L2half+B+R2, 0, 0
K, 51, L2half+B+R2, 0, 5*L1division
K, 52, L2half+B1+R2, 0, 5*L1division
K, 53, L2half+B1+R2, 0, hdist_vert - 5*L1division
K, 54, L2half+B+R2, 0, hdist_vert - 5*L1division
K, 55, L2half+B+R2, 0, hdist_vert + 5*L1division
K, 56, L2half+B1+R2, 0, hdist_vert + 5*L1division
K, 57, L2half+B1+R2, 0, 2*hdist_vert - 5*L1division
K, 58, L2half+B+R2, 0, 2*hdist_vert - 5*L1division
K, 59, L2half+B+R2, 0, 2*hdist_vert + 5*L1division
K, 60, L2half+B1+R2, 0, 2*hdist_vert + 5*L1division
K, 61, L2half+B+R2, 0, L1half
K, 62, L2half+B+R2, 0, L1half+(R2+B1)*COS45
K, 63, L2half+B+R2, 0, L1half+R2+B
K, 64, L2half+(R2+B1)*COS45, 0, L1half+B+R2
K, 65, L2half, 0, L1half+B+R2

! Point in the middle of the fillet
CSYS, 12
K, 66, R2+B1, 45,  0
CSYS,0

K, 67, L2half-7*L2division, 0, L1half+B+R2
K, 68, L2half-7*L2division, 0, L1half+B1+R2
K, 69, 4*L2division, 0, L1half+B+R2
K, 70, 4*L2division, 0, L1half+B1+R2
K, 71, 0, 0, L1half+B+R2

L, 50, 51	!65
L, 52, 2	!66
L, 51, 52	!67
L, 53, 54	!68
L, 53, 52	!69
L, 54, 51	!70
L, 54, 55	!71
L, 55, 56	!72
L, 56, 53	!73
L, 57, 56	!74
L, 57, 58	!75
L, 58, 55	!76
L, 58, 59	!77
L, 59, 60	!78
L, 60, 57	!79
L, 59, 61	!80
L, 61, 10	!81
L, 10, 60	!82

L, 61, 62	!83
L, 62, 66	!84
L, 62, 63	!85
L, 63, 64	!86
L, 64, 66	!87
L, 64, 65	!88
L, 65, 18	!89
L, 65, 67	!90
L, 67, 68	!91
L, 68, 18	!92
L, 68, 70	!93
L, 67, 69	!94
L, 69, 70	!95
L, 70, 26	!96
L, 69, 71	!97

! To match the longitudinal lines nodes it's necessary to 
! dived the section in a precise length:

LSEL,S,LINE,,65,82
LSEL,A,LINE,,85,86
LSEL,A,LINE,,89,97
LESIZE,ALL,L1division

! While for line that must match with different length it's
! necessary divide the line in a precise number of elements
LSEL,S,LINE,,83,88,5
LESIZE,ALL,,,Ndivl1
LSEL,S,LINE,,84,87,3
LESIZE,ALL,,,8
ALLSEL,ALL

! >>> Holes <<<
! -- Longitudinal holes: --
! C1

LOCAL, 13, 1, x_ctr+R2, 0, 0,, 90
CSYS, 13

K, 100, Rh				
K, 101, Rh, 45				 
K, 102, Rh, 135
K, 103, Rh,180	
	
L, 100, 101	!98
L, 101, 102	!99
L, 102, 103	!100
CSYS, 0

! C2
LOCAL, 14, 1, x_ctr+R2, 0, hdist_vert,, 90
CSYS, 14

K, 104, Rh, 45
K, 105, Rh, 135
K, 106, Rh, 225
K, 107, Rh, 315

L, 104, 105	!101
L, 105, 106	!102
L, 106, 107	!103
L, 107, 104	!104
CSYS, 0

! C3	
LOCAL, 15, 1, x_ctr+R2, 0, 2*hdist_vert,, 90
CSYS, 15 

K, 108, Rh, 45
K, 109, Rh, 135
K, 110, Rh, 225
K, 111, Rh, 315

L, 108, 109	!105
L, 109, 110	!106
L, 110, 111	!107
L, 111, 108	!108
CSYS,0

! -- Corner hole --
! C4
LOCAL, 16, 1, x_ctr+R2, 0, z_ctr+R2,, 90
CSYS, 16 
K, 112, Rh, 45
K, 113, Rh, 135
K, 114, Rh, 225
K, 115, Rh, 315

L, 112, 113	!109
L, 113, 114	!110
L, 114, 115	!111
L, 115, 112	!112
CSYS,0

! -- Transverse hole --

! C5
LOCAL, 17, 1, hdist_hor, 0, z_ctr+R2,, 90
CSYS, 17 
K, 116, Rh, 45
K, 117, Rh, 135
K, 118, Rh, 225
K, 119, Rh, 315

L, 116, 117	!113
L, 117, 118	!114
L, 118, 119	!115
L, 119, 116	!116
CSYS,0

! C6
LOCAL, 18, 1,  0, 0, z_ctr+R2,, 90
CSYS, 18 
K, 120, Rh, -90
K, 121, Rh, -45
K, 122, Rh, 45
K, 123, Rh, 90

L, 120, 121	!117
L, 121, 122	!118
L, 122, 123	!119

CSYS,0


! Meshing the holes
! Since the division of the lines must match with the opposite one
! the holes have different division
LSEL,S,LINE,,98, 100,2		
LESIZE,ALL,,,L1division/2
LSEL,S,LINE,,117, 119,2		
LESIZE,ALL,,,4
LSEL,S,LINE,,99,107,2		
LSEL,A,LINE,,110,111
LSEL,A,LINE,,114,118,2
LESIZE,ALL,,,8
LSEL,S,LINE,,102,108,2
LSEL,A,LINE,,109,112,3
LESIZE,ALL,,,L1division
LSEL,S,LINE,,113,115,2
LESIZE,ALL,,,7
LSEL,S,LINE,,117,119,2
LESIZE,ALL,,,4
ALLSEL,ALL

! Crossing lines 
L, 2, 103	!120
L, 100,50	!121
L, 51, 101	!122
L, 52,102	!123
L, 53, 106	!124
L, 54, 107	!125
L, 55, 104	!126
L, 56, 105	!127
L, 57, 110	!128
L, 58, 111	!129
L, 59, 108	!130
L, 60, 109	!131
L, 62, 115	!132
L, 63, 112	!133
L, 64, 113	!134
L, 66, 114	!135
L, 18, 119	!136
L, 65, 116	!137
L, 67, 117	!138
L, 68, 118	!139
L, 69, 122	!140
L, 70, 121	!141
L, 71, 123	!142
L, 26, 120	!143

LSEL,S,LINE,,120,143,1
LESIZE,ALL,,,Ndivl2
ALLSEL,ALL

! Curved lines in the middle of the fillet
CSYS,12
L,10,66		!144
L,66,18		!145
CSYS,0
LSEL,S,LINE,,144,145
LESIZE,ALL,,,Ndivl1
ALLSEL,ALL

! Areas
AL, 121, 65, 122, 98	!43
AL, 122, 67, 123, 99
AL, 66, 123, 100, 120
AL, 67, 70, 68, 69
AL, 68, 125,103,124
AL, 125, 71, 126,104
AL, 126, 101, 127,72
AL, 127, 102, 124,73
AL, 72, 76, 75, 74
AL, 75, 129, 107, 128
AL, 129, 77, 130, 108
AL, 130, 78, 131, 105
AL, 131, 79, 128, 106
AL, 78, 80, 81, 82
AL, 81, 83, 84, 144
AL, 84, 132, 111, 135
AL, 132, 85, 133, 112
AL, 133, 86, 134, 109
AL, 135, 110, 134, 87
AL, 87, 145, 89, 88
AL, 89, 136, 116,137
AL, 137, 90, 138, 113
AL, 136, 92, 139, 115
AL, 139, 91, 138, 114
AL, 91, 93, 95, 94
AL, 95, 141, 118, 140
AL, 97, 142, 119, 140
AL, 117, 143, 96, 141	!70

! Extrusion of the plate
ASEL,S,AREA,,43,70
VDRAG,ALL,,,,,,2
ALLSEL,ALL

NUMMRG,ALL
NUMCMP,ALL

!------------------------------------!
!!>>>>>>>>>> PLATE MESH <<<<<<<<<<<<!!
!------------------------------------!

! Specchiare tutte le facce
VSYMM,X, ALL,,,,0,0
ALLSEL
VSYMM,z, ALL,,,,0,0
ALLSEL

TYPE,1
MAT,1
MSHAPE,0,3D
MSHKEY,1
VMESH,ALL

NUMMRG,NODE,0.1,,SELE
NUMMRG,ALL
ALLS

NUMCMP,VOLU


! ---- CONTACT ---- 

ET,4,TARGE170
ET,5,CONTA174
KEYOPT, 5, 2, 0 !Contact algorithm: Augmented Lagrangian
KEYOPT, 5, 7, 1 !Element level time incrementation control: Automatic bisection of increment
KEYOPT, 5, 9, 2 !Include both initial geometrical penetration or gap and offset, but with ramped effects
KEYOPT, 5, 10, 2 !Contact stiffness update: Each iteration

TYPE, 5
REAL, 1
NSEL, S, LOC, Y, 0
ESURF
ALLSEL, ALL

K, 5000, -300, 0, 600
K, 5001, -300, 0, -600
K, 5002, 300, 0, -600
K, 5003, 300, 0, 600

L, 5000, 5001
nline = _return
L, 5001, 5002
L, 5002, 5003
L, 5003, 5000

AL, nline, nline+1, nline+2, nline+3
LSEL, S, LINE,, nline, nline+3
LESIZE, ALL,,, Ndivl3

ASLL, S, AREA,, nline, nline+3
TYPE, 4
REAL, 1
ESIZE, 10
AMESH, ALL

NSLA, S, ALL
D, ALL, ALL

ALLS

!------------------------------------!
!!>>>>>>>>>>>>> M27 Nut <<<<<<<<<<<<!!
!------------------------------------!

! --- N1 ---
LOCAL, 2001, 1, x_ctr+R2, tko, hdist_vert,, -90
CSYS, 2001
K, 2010, 0, 0, 0			! To define the axis
K, 2011, 0, 0, nut_height	! To define the axis

! Outer points
K, 2012, nut_ext_radius, 0, 0
K, 2013, nut_ext_radius, 60, 0
K, 2014, nut_ext_radius, 120, 0
K, 2015, nut_ext_radius, 180, 0
K, 2016, nut_ext_radius, 240, 0
K, 2017, nut_ext_radius, 300, 0


! Inner points
K, 2018, nut_hole_radius, 0, 0
K, 2019, nut_hole_radius, 60, 0
K, 2020, nut_hole_radius, 120, 0
K, 2021, nut_hole_radius, 180, 0
K, 2022, nut_hole_radius, 240, 0
K, 2023, nut_hole_radius, 300, 0

L, 2018, 2019
L, 2019, 2020
L, 2020, 2021
L, 2021, 2022
L, 2022, 2023
L, 2023, 2018

CSYS, 0
L, 2012, 2013
L, 2013, 2014
L, 2014, 2015
L, 2015, 2016
L, 2016, 2017
L, 2017, 2012

L, 2010, 2011
naxis = _return 

!Nut lines division
LSEL,S,LINE,,naxis-13, naxis-1
LESIZE,ALL,,,L1division/2
LSEL,S,LINE,,naxis
LESIZE,ALL,,,Ndivtk
ALLSEL,ALL

! Extrude the full volume
KSEL, S, KP,, 2012, 2017
LSLK, S, 0
AL, ALL
ASLL, S, 0
VDRAG, ALL,,,,,, naxis
vout = _return

ALLSEL

! Extrude the inner volume
KSEL, S, KP,, 2018, 2023
LSLK, S, 0
AL, ALL
ASLL, S, 0

VDRAG, ALL,,,,,, 2
vin = _return

VADD, vout, vin
NUMCMP,VOLU
ALLSEL

!-------NUT MESH------!
TYPE,1
MAT,1
MSHAPE,1,3D
MSHKEY,0
VMESH,vout

NUMCMP,AREA

! Copy volumes
! --- N2 ---
LOCAL, 2002, 1, x_ctr+R2, tko, 2*hdist_vert,, -90
CSYS, 2001
VTRAN, 2002, vout
CSYS,0

! --- N3 ---
LOCAL, 2003, 1, x_ctr+R2, tko, z_ctr+R2,, -90
CSYS, 2001
VTRAN, 2003, vout
CSYS, 0

! --- N4 ---
LOCAL, 2004, 1, hdist_hor, tko, z_ctr+R2,, -90
CSYS, 2001
VTRAN, 2004, vout
CSYS, 0

! --- N5 ---
LOCAL, 2005, 1, 0, tko, z_ctr+R2,, -90
CSYS, 2001
VTRAN, 2005, vout
CSYS, 0

! --- N6 ---
LOCAL, 2006, 1, -hdist_hor, tko, z_ctr+R2,, -90
CSYS, 2001
VTRAN, 2006, vout
CSYS, 0

! --- N7 ---
LOCAL, 2007, 1, -x_ctr-R2, tko, z_ctr+R2,, -90
CSYS, 2001
VTRAN, 2007, vout
CSYS, 0

! --- N8 ---
LOCAL, 2008, 1,  -x_ctr-R2, tko, 2*hdist_vert,, -90
CSYS, 2001
VTRAN, 2008, vout
CSYS, 0

! --- N9 ---
LOCAL, 2009, 1, -x_ctr-R2, tko, hdist_vert,, -90
CSYS, 2001
VTRAN, 2009, vout
CSYS, 0

! --- N10 ---
LOCAL, 2010, 1, -x_ctr-R2, tko, 0,, -90			!--------------------------------------------
CSYS, 2001
VTRAN, 2010, vout
CSYS, 0

! --- N11 ---
LOCAL, 2011, 1, -x_ctr-R2, tko, -hdist_vert,, -90
CSYS, 2001
VTRAN, 2011, vout
CSYS, 0

! --- N12 ---
LOCAL, 2012, 1,  -x_ctr-R2, tko, -2*hdist_vert,, -90
CSYS, 2001
VTRAN, 2012, vout
CSYS, 0

! --- N13 ---
LOCAL, 2013, 1, -x_ctr-R2, tko, -z_ctr-R2,, -90
CSYS, 2001
VTRAN, 2013, vout
CSYS, 0

! --- N14 ---
LOCAL, 2014, 1, -hdist_hor, tko, -z_ctr-R2,, -90
CSYS, 2001
VTRAN, 2014, vout
CSYS, 0

! --- N15 ---
LOCAL, 2015, 1, 0, tko, -z_ctr-R2,, -90
CSYS, 2001
VTRAN, 2015, vout
CSYS, 0

! --- N16 ---
LOCAL, 2016, 1, hdist_hor, tko, -z_ctr-R2,, -90
CSYS, 2001
VTRAN, 2016, vout
CSYS, 0

! --- N17 ---
LOCAL, 2017, 1, x_ctr+R2, tko, -z_ctr-R2,, -90
CSYS, 2001
VTRAN, 2017, vout
CSYS, 0

! --- N18 ---
LOCAL, 2018, 1, x_ctr+R2, tko, -2*hdist_vert,, -90
CSYS, 2001
VTRAN, 2018, vout
CSYS,0

! --- N19 ---
LOCAL, 2019, 1, x_ctr+R2, tko, -hdist_vert,, -90
CSYS, 2001
VTRAN, 2019, vout
CSYS, 0

! --- N20 ---
LOCAL, 2020, 1, x_ctr+R2, tko, 0,, -90
CSYS, 2001
VTRAN, 2020, vout
CSYS, 0


!-------CONTACT MESH----!


*DO,i,2001,2020,1

TYPE,5
REAL,i-2000+1

CSYS,i
NSEL,S,LOC,X,Rh,2.5*Rh
NSEL,R,LOC,Z,0
ESURF
ALLSEL,all

TYPE,4
REAL,i-2000+1
ASEL,S,LOC,X,0,2*Rh
ASEL,R,LOC,Z,0
NSLA,S,1
ESURF
ALLSEL,all


*ENDDO

CSYS,0
ALLSEL,ALL

! ---- CONSTRAINTS ---- 

! !Fixed base
! NSEL,S,loc,y,-eps,+eps
! D, ALL,uy

! *DO,i,2001,2020,1

! CSYS,i
! ASEL,S,LOC,X,0,rh,rh+eps
! ASEL,R,LOC,Z,-30,-eps
! NSLA,S,1
! D, ALL,ux,Eps,,,,uz

! *ENDDO
! CSYS,0
! allsel,all

!Fix displ. of the nuts (base of the screws)

*DO,i,2001,2020,1

CSYS,i
NSEL,S,LOC,X,0,nut_hole_radius
NSEL,R,LOC,Z,-tko+eps,-tko-eps
D, ALL,ALL

*ENDDO

CSYS,0
ALLS

! --- MPC LOADS ---

*GET, max_node, NODE, 0, NUM, MAXD
N,max_node+10,0,1.1*H,0 !create 'master' node for MPC184's
ET,6,184
KEYOPT,6,1,1
TYPE,6

NSEL, S, LOC, Y, H-eps,H+eps
*GET, num_nodes, NODE, 0, COUNT
*DO, ii, 1, num_nodes,1
*GET, n_node, NODE, 0, NUM, MAX
E,max_node+10,n_node
NSEL, U, NODE, , n_node
*ENDDO
ALLSEL, ALL

F,max_node+10,MZ,Mfz
F,max_node+10,MX,Mfx
F,max_node+10,My,Mty

LSWRITE,1

FINISH

!------------------------------------!
!!>>>>>>>>>>>> SOLUTION <<<<<<<<<<<<!!
!------------------------------------!

/SOLU

TIME,1
NSUBST,5
eqslv,pcg

*DO,i,2001,2020,1

CSYS,i
ASEL,S,LOC,X,0,nut_hole_radius
ASEL,R,LOC,Z,nut_height 
*GET, narea, AREA, 0, NUM, MAX
SLOAD,narea,PL01,lock, forc,preload,1,2
*ENDDO

CSYS,0

MODE,0,0
NSEL,S,LOC,Y,H-eps,H+eps
SF, ALL, PRES, Sigmay
ALLSEL,ALL

LSWRITE,1
LSSOLVE,1

