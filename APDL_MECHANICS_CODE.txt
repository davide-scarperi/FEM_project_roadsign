
FINISH
/CLEAR, START, NEW
/FILNAM,road_sign
/TITLE,simply first version

! >>>>> PILLAR PARAMETERS <<<<<

length1= 6800			! pillar length, mm
B1 = 300			! low cross section width, mm
H1 = 900			! low cross section height, mm
B2 = 550			! high cross section width, mm
H2 = 600			! high cross section height, mm
T1 = 10				! thickness, mm

! >>>>> HORIZONTAL BEAM PARAMETERS <<<<<

length2= 1390			! first beam length, mm
length3= 6000			! second beam length, mm
B3 = 400			! high cross section width, mm
H3 = 600			! high cross section height, mm
T2 = 8				! thickness, mm

! >>>>> PLATE PARAMETERS <<<<<
T3 = 30				! thickness, mm

! >>>>> RACKETS PARAMETERS <<<<<

length4= 1200			! racket length, mm
length5= 3100			! racket length, mm
B4 = 60				! cross section width, mm
H4 = 100			! cross section height, mm

length6 = 2130			! distances, mm
length7 = 350			! distances, mm
length8 = 800			! distances, mm
length9 = 850			! distances, mm


! >>>>> MATERIAL PROPERTIES <<<<<
E_Young = 210000		! Young's modulus, MPa
ni = 0.29			! Poisson's ratio
Density=7.85e-6		! Kg/mm3


! >>>>> other parameters <<<<<
UpF = 7690	   ! load (N/mm)
LwF = 3130	   ! load (N/mm)
E_length = 100	! element length (mm)
Eps = 1E-4	! selection tolerance

/PREP7
ET, 1, BEAM189
!section of low pillar
SECTYPE,   1, BEAM, HREC, low_pillar, 0  
SECOFFSET, USER, 150, 450   	! Change the origin axis point
SECDATA,B1,H1,T1,T1,T1,T1,0,0,0,0,0,0 

!section of high pillar
SECTYPE,   2, BEAM, HREC, high_pillar, 0  
SECOFFSET, USER, 400, 300   	! Change the origin axis point
SECDATA,B2,H2,T1,T1,T1,T1,0,0,0,0,0,0 

!section of horizontal beam
SECTYPE,   3, BEAM, HREC, horizontal_beam, 0   
SECDATA,B3,H3,T2,T2,T2,T2,0,0,0,0,0,0 

!Section of taper beam
SECTYPE,   4, TAPER, , taper_pillar
SECDATA,   1,,,,
SECDATA,   2,0,length1,,

!Section of the rackets
SECTYPE,   5, BEAM, RECT,racket,0
SECDATA,B4,H4

!Material definition
MP, EX, 1, E_Young
MP, PRXY, 1, ni
MP,dens,1,density

!keypoints pillar
K,1,0,0,0
K,2,0,length1,0 
L, 1, 2  

LESIZE, all,E_length
LATT,1,,1,,,,4
LMESH, 1

Lsel,u,line,,all

!keypoint first h. beam
K,3, -B1/2, length1+B3/2,0
K,4, 0, length1+B3/2,0
K,5, length2+length6+length7, length1+B3/2,0
K,6, length2+length6+length7+length8, length1+B3/2,0
K,7, length2+length6+length7+length8+length9, length1+B3/2,0
K,8, length2+length6+length7+length8+2*length9, length1+B3/2,0
K,9, length2+length6+length7+2*length8+2*length9, length1+B3/2,0
K,10,length2+2*T3+length3,length1+B3/2,0
L,3,4
L, 4, 5
L, 5, 6
L, 6, 7
L, 7, 8
L, 8, 9
L, 9, 10

LESIZE,all,E_length
Latt,1,,1,,,,3
LMESH,all
Lsel,u,line,,all

! >>>> 	view setting: 	<<<<
/SHRINK,0   
/ESHAPE,1.0 
/EFACET,1   
/RATIO,1,1,1
/CFORMAT,32,0   
/REPLOT 



!keypoints for rackets

K,11,length2+length6+length7,length1+B3/2,H3/2+H4/2
K,12,length2+length6+length7,length1+B3/2-length4/2,H3/2+H4/2	!lower point of force
K,13,length2+length6+length7,length1+B3/2-length4,H3/2+H4/2
K,14,length2+length6+length7,length1+B3/2+length5/2,H3/2+H4/2	!upper point of force
K,15,length2+length6+length7,length1+B3/2+length5,H3/2+H4/2

L, 11, 12
L, 12, 13
L, 11, 14
L, 14, 15

K,16,length2+length6+length7+length8,length1+B3/2,H3/2+H4/2
K,17,length2+length6+length7+length8,length1+B3/2-length4/2,H3/2+H4/2	!lower point of force
K,18,length2+length6+length7+length8,length1+B3/2-length4,H3/2+H4/2
K,19,length2+length6+length7+length8,length1+B3/2+length5/2,H3/2+H4/2	!upper point of force
K,20,length2+length6+length7+length8,length1+B3/2+length5,H3/2+H4/2

L, 16, 17
L, 17, 18
L, 16, 19
L, 19, 20

K,21,length2+length6+length7+length8+length9,length1+B3/2,H3/2+H4/2
K,22,length2+length6+length7+length8+length9,length1+B3/2-length4/2,H3/2+H4/2	!lower point of force
K,23,length2+length6+length7+length8+length9,length1+B3/2-length4,H3/2+H4/2
K,24,length2+length6+length7+length8+length9,length1+B3/2+length5/2,H3/2+H4/2	!upper point of force
K,25,length2+length6+length7+length8+length9,length1+B3/2+length5,H3/2+H4/2

L, 21, 22
L, 22, 23
L, 21, 24
L, 24, 25

K,26,length2+length6+length7+length8+2*length9,length1+B3/2,H3/2+H4/2
K,27,length2+length6+length7+length8+2*length9,length1+B3/2-length4/2,H3/2+H4/2	!lower point of force
K,28,length2+length6+length7+length8+2*length9,length1+B3/2-length4,H3/2+H4/2
K,29,length2+length6+length7+length8+2*length9,length1+B3/2+length5/2,H3/2+H4/2	!upper point of force
K,30,length2+length6+length7+length8+2*length9,length1+B3/2+length5,H3/2+H4/2

L, 26, 27
L, 27, 28
L, 26, 29
L, 29, 30

K,31,length2+length6+length7+2*length8+2*length9,length1+B3/2,H3/2+H4/2
K,32,length2+length6+length7+2*length8+2*length9,length1+B3/2-length4/2,H3/2+H4/2	!lower point of force
K,33,length2+length6+length7+2*length8+2*length9,length1+B3/2-length4,H3/2+H4/2
K,34,length2+length6+length7+2*length8+2*length9,length1+B3/2+length5/2,H3/2+H4/2	!upper point of force
K,35,length2+length6+length7+2*length8+2*length9,length1+B3/2+length5,H3/2+H4/2

L, 31, 32
L, 32, 33
L, 31, 34
L, 34, 35

LESIZE,all,E_length
Latt,1,,1,,,,5
LMESH,all
Lsel,u,line,,all


!keypoints for rigid plates
K, 36,0,length1,0 
K, 37,length2+length6+length7,length1+B3/2,H3/2+H4/2
K, 38,length2+length6+length7+length8,length1+B3/2,H3/2+H4/2
K, 39,length2+length6+length7+length8+length9,length1+B3/2,H3/2+H4/2
K, 40,length2+length6+length7+length8+2*length9,length1+B3/2,H3/2+H4/2
K, 41,length2+length6+length7+2*length8+2*length9,length1+B3/2,H3/2+H4/2
L, 36, 4
L, 37, 5
L, 38, 6
L, 39, 7
L, 40, 8
L, 41, 9

ET,2,184,1,1
TYPE,2
LESIZE, all,E_length
LMESH, all
allsel,all


!use of CPINTF     !Internal pins
CPINTF,ux,eps
CPINTF,uy,eps
CPINTF,uz,eps
CPINTF,rotx,eps
CPINTF,roty,eps
CPINTF,rotz,eps

save
Fini

/SOLUTION
ANTYPE, STATIC, NEW   !static solution

!Fixed base
NSEL,S,loc,y,-eps,+eps
D, ALL,ALL
allsel,all

ksel,s,kp,,12,32,5
nslk,s
F,all,Fz,-LwF

ksel,s,kp,,14,34,5
nslk,s
F,all,Fz,-UpF

ALLSEL, all
!Gravitational acceleration
Acel,,9.81	!m/s2



allsel,all,all
solve
save
fini